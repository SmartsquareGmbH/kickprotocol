apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "maven-publish"

afterEvaluate {
    generateReleaseBuildConfig.enabled = false
}

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    api "com.google.android.gms:play-services-nearby:$playServicesVersion"

    api "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    api "com.squareup.moshi:moshi:$moshiVersion"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        library(MavenPublication) {
            artifactId 'kickprotocol'

            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/library-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.api.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')

                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
